DESCRIPTION = "CompuLab i.MX8 U-Boot"
require recipes-bsp/u-boot/u-boot.inc

PROVIDES += "u-boot"
DEPENDS:append = " dtc-native"

LICENSE = "GPLv2+"
LIC_FILES_CHKSUM = "file://Licenses/gpl-2.0.txt;md5=b234ee4d69f5fce4486a80fdaf4a4263"

require u-boot-compulab_${PV}.inc

FILESEXTRAPATHS:prepend := "${THISDIR}/compulab/${PV}/imx8mp:"
require compulab/${PV}/imx8mp.inc

DEPENDS += "flex-native bison-native bc-native dtc-native gnutls-native"

S = "${WORKDIR}/git"
B = "${WORKDIR}/build"

inherit fsl-u-boot-localversion

LOCALVERSION = "-compulab"

BOOT_TOOLS = "imx-boot-tools"

do_merge_config () {
    cd ${S}
    cp ${S}/configs/${MACHINE}_defconfig ${S}/configs/compulab_defconfig
    ${S}/scripts/kconfig/merge_config.sh  -O ${S}/configs/ -m ${S}/configs/${MACHINE}_defconfig ${WORKDIR}/*.cfg
    mv ${S}/configs/.config ${S}/configs/${MACHINE}_defconfig
    cd -
    oe_runmake ${MACHINE}_defconfig
    mv ${S}/configs/compulab_defconfig ${S}/configs/${MACHINE}_defconfig
}

do_compile:prepend () {
    export SOURCE_DATE_EPOCH=$(date +%s)
    do_merge_config
}

do_deploy:append () {
    # Deploy u-boot-nodtb.bin and fsl-imx8mq-XX.dtb, to be packaged in boot binary by imx-boot
    if [ -n "${UBOOT_CONFIG}" ]
    then
        for config in ${UBOOT_MACHINE}; do
            i=$(expr $i + 1);
            for type in ${UBOOT_CONFIG}; do
                j=$(expr $j + 1);
                if [ $j -eq $i ]
                then
                    install -d ${DEPLOYDIR}/${BOOT_TOOLS}
                    install -m 0777 ${B}/${config}/arch/arm/dts/${UBOOT_DTB_NAME}  ${DEPLOYDIR}/${BOOT_TOOLS}
                    install -m 0777 ${B}/${config}/u-boot-nodtb.bin  ${DEPLOYDIR}/${BOOT_TOOLS}/u-boot-nodtb.bin-${MACHINE}-${UBOOT_CONFIG}
                fi
            done
            unset  j
        done
        unset  i
    fi

}

require u-boot-compulab-env.inc

PACKAGE_ARCH = "${MACHINE_ARCH}"
UBOOT_NAME = "u-boot-${MACHINE}.bin-${UBOOT_CONFIG}"
